dependencies {
	compile project(':org.eclipse.xtext.ide')
	compile project(':org.eclipse.xtext.testing')
	compile project(':org.eclipse.xtext.xtext.generator')
	compile "junit:junit:$versions.junit"
	compile "io.typefox.lsapi:io.typefox.lsapi.services:$versions.lsapi"
	mwe2 "org.eclipse.emf:org.eclipse.emf.mwe2.launch:$versions.emfMwe2"
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.common.types:$versions.xtext_bootstrap"
}


task generateXtextLanguage(type: JavaExec) {
	main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
	classpath = configurations.mwe2
	inputs.file "testlang-src/org/eclipse/xtext/ide/tests/testlanguage/GenerateTestLanguage.mwe2"
	inputs.file "testlang-src/org/eclipse/xtext/ide/tests/testlanguage/TestLanguage.xtext"
	outputs.dir "testlang-src-gen"
	args += "testlang-src/org/eclipse/xtext/ide/tests/testlanguage/GenerateTestLanguage.mwe2"
	args += "-p"
	args += "rootPath=/${projectDir}/.."
}

compileJava.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]

sourceSets.test.java.srcDirs = ['testlang-src', 'testlang-src-gen'] + sourceSets.test.java.srcDirs
sourceSets.test.resources.srcDirs += ['testlang-src', 'testlang-src-gen']